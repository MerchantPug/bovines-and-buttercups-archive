import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    id "net.darkhax.curseforgegradle" version "1.0.7"
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${artifact_name}-Fabric"

repositories {
    maven {
        url "https://maven.terraformersmc.com/"
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven { url "https://maven.shedaniel.me/" }
    mavenCentral()
}


loom {
    accessWidenerPath.set(file("src/main/resources/bovinesandbuttercups.accesswidener"))
    mixin {
        defaultRefmapName.set("bovinesandbuttercups.refmap.json")
    }

    mods {
        register(mod_id) {
            sourceSet(sourceSets.main)
            sourceSet(sourceSets.test)
        }
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation("com.terraformersmc:modmenu:${modmenu_version}") {
        exclude(group: "net.fabricmc")
    }

    include(modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}")) {
        exclude(group: "net.fabricmc")
    }
    include(modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}")) {
        exclude(group: "net.fabricmc")
    }

    include(modApi(annotationProcessor("io.github.llamalad7:mixinextras-fabric:${mixin_extras_version}")))

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'
    annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'

    implementation(project(":common"))

    modCompileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}") { transitive false }
    modCompileOnly("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}") { transitive false }

    // modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}") { transitive false }
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}") { transitive false }
    modCompileOnly("dev.architectury:architectury-fabric:${architectury_version}") { transitive false }
    modCompileOnly("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") { transitive false }

    /*
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}") {
        exclude module: 'dev.architectury'
    }
    modRuntimeOnly "dev.architectury:architectury-fabric:${architectury_version}"
     */

    modCompileOnly("dev.emi:emi-fabric:${emi_version}:api") { transitive false }
    // modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from(project(":common").sourceSets.main.resources)
    exclude("bovinesandbuttercups-common.accesswidener")
}

processTestResources {
    from(project(":common").sourceSets.test.resources)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}

tasks.register('curseforge', TaskPublishCurseForge) {
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(cf_project_id, remapJar)
    mainFile.displayName = "Fabric v${rootProject.version}"
    mainFile.releaseType = "release"
    mainFile.changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    mainFile.changelogType = "markdown"
    mainFile.addModLoader('Fabric')
    java_versions.split(", ").each {
        mainFile.addJavaVersion(it)
    }

    fabric_game_versions.split(", ").each {
        mainFile.addGameVersion(it)
    }

    fabric_required_deps.split(", ").each {
        mainFile.addRequirement(it)
    }

    if (project.hasProperty("common_optional_deps")) {
        common_optional_deps.split(", ").each {
            mainFile.addOptional(it)
        }
    }

    if (project.hasProperty("cf_common_optional_deps")) {
        cf_common_optional_deps.split(", ").each {
            mainFile.addOptional(it)
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = mr_project_id
    versionNumber = "${project.version}-fabric"
    versionType = "release"
    versionName = "Fabric v$project.version"
    changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    uploadFile = remapJar
    gameVersions = fabric_game_versions.split(", ").toList()
    loaders = [ "fabric" ]
    dependencies {
        fabric_required_deps.split(", ").each { String project ->
            required.project project
        }

        if (project.hasProperty("common_optional_deps")) {
            common_optional_deps.split(", ").each { String project ->
                optional.project project
            }
        }

        if (project.hasProperty("mr_common_optional_deps")) {
            mr_common_optional_deps.split(", ").each { String project ->
                optional.project project
            }
        }
    }
}