plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
    id 'maven-publish'
}

archivesBaseName = "${mod_name}-xplat"

minecraft {
    version(minecraft_version)
    runs {   
        if (project.hasProperty('xplat_runs_enabled') ? project.findProperty('xplat_runs_enabled').toBoolean() : true) {
        
            server(project.hasProperty('xplat_server_run_name') ? project.findProperty('xplat_server_run_name') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }
            client(project.hasProperty('xplat_client_run_name') ? project.findProperty('xplat_client_run_name') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }
    accessWideners "src/main/resources/bovinesandbuttercupsxplat.accesswidener"
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name:'mixin', version:'0.8.5'
    compileOnly group: 'org.ow2.asm', name: 'asm-tree', version: '9.3'

    compileOnly("com.github.LlamaLad7:MixinExtras:${mixin_extras_version}")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:${mixin_extras_version}")

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
}

processResources {

    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials(PasswordCredentials)
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}