plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
    id 'maven-publish'
}

archivesBaseName = "${artifact_name}-Common"

minecraft {
    version(minecraft_version)
    runs {   
        if (project.hasProperty('common_runs_enabled') ? project.findProperty('common_runs_enabled').toBoolean() : true) {
        
            server(project.hasProperty('common_server_run_name') ? project.findProperty('common_server_run_name') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }
            client(project.hasProperty('common_client_run_name') ? project.findProperty('common_client_run_name') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }
    accessWideners "src/main/resources/bovinesandbuttercups-common.accesswidener"
}

repositories {
    maven {
        url 'https://jitpack.io'
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name:'mixin', version:'0.8.5'
    compileOnly group: 'org.ow2.asm', name: 'asm-tree', version: '9.4'

    compileOnly("com.github.LlamaLad7:MixinExtras:${mixin_extras_version}")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:${mixin_extras_version}")

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    
    // TODO: Reimplement JEI
    // compileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
}

// Based on this Gist
// https://gist.github.com/mosamman/eaa92795b300225c6e499c0fb3d6ef6f

// Only publish to maven if an artifact cannot be found
publishMavenJavaPublicationToMerchantpugRepository.onlyIf {
    def connection = (HttpURLConnection) new URL("https://maven.merchantpug.net/releases/net/merchantpug/${archivesBaseName}/${version}/${version}.pom")

    connection.setRequestProperty("Authorization", "Basic " + (System.getenv("ORG_GRADLE_PROJECT_merchantpugUsername") + ":" + System.getenv("ORG_GRADLE_PROJECT_merchantpugPassword")).bytes.encodeBase64().toString())
    connection.setConnectTimeout(10000)
    connection.setReadTimeout(10000)
    connection.setRequestMethod("HEAD")

    def responseCode = connection.getResponseCode()

    if (responseCode == 404) {
        return true
    }

    return false
}

processResources {

    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials(PasswordCredentials)
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}