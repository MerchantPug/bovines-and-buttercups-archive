plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
}

archivesBaseName = "${artifact_name}-Fabriclike"

repositories {
    maven {
        url "https://maven.terraformersmc.com/"
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modCompileOnly "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"

    compileOnly "com.github.LlamaLad7:MixinExtras:${mixin_extras_version}"
    annotationProcessor "com.github.LlamaLad7:MixinExtras:${mixin_extras_version}"

    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    implementation project(":Common")

    // TODO: Reimplement JEI
    // modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    // modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}"

    modCompileOnly "dev.emi:emi:${emi_version}"
}

// Based on this Gist
// https://gist.github.com/mosamman/eaa92795b300225c6e499c0fb3d6ef6f

// Only publish to maven if an artifact cannot be found
publish.onlyIf {
    def connection = (HttpURLConnection) new URL("https://maven.merchantpug.net/releases/net/merchantpug/${archivesBaseName}/${version}/${version}.pom").openConnection()

    connection.setRequestProperty("Authorization", "Basic " + (System.getenv("ORG_GRADLE_PROJECT_merchantpugUsername") + ":" + System.getenv("ORG_GRADLE_PROJECT_merchantpugPassword")).bytes.encodeBase64().toString())
    connection.setConnectTimeout(10000)
    connection.setReadTimeout(10000)
    connection.setRequestMethod("HEAD")

    def responseCode = connection.getResponseCode()

    if (responseCode == 404) {
        return true
    }

    return false
}

loom {
    accessWidenerPath = file("src/main/resources/bovinesandbuttercups-fabriclike.accesswidener")
    mixin {
        defaultRefmapName = "bovinesandbuttercups-refmap.json"
    }
}

processResources {
    from project(":Common").sourceSets.main.resources

    exclude("bovinesandbuttercups-common.accesswidener")

    inputs.property "version", project.version

    def environment = System.getenv("ENVIRONMENT")
    if (environment != "production") {
        from(rootProject.rootDir.absolutePath + "/testdata") { into 'data' }
        from(rootProject.rootDir.absolutePath + "/testassets") { into 'assets'}
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials(PasswordCredentials)
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}
