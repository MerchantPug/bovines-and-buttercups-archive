plugins {
    id 'org.quiltmc.loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
}

archivesBaseName = "${project.mod_name}-Quilt"

repositories {
    maven {
        name 'Quilt (Release)'
        url 'https://maven.quiltmc.org/repository/release'
    }

    maven {
        name 'Quilt (Snapshot)'
        url 'https://maven.quiltmc.org/repository/snapshot'
        content {
            includeGroup 'org.quiltmc.fabric_api_qsl'
            includeGroupByRegex 'org\\.quiltmc\\.qsl(?:\\.[a-z_]+)?'
        }
    }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
    maven {
        url 'https://maven.terraformersmc.com/'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"

    modImplementation("org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilt_stdlib_version}") {
        exclude group: "net.fabricmc"
    }

    modImplementation "maven.modrinth:inspecio:${inspecio_version}"
    modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"

    implementation "com.github.LlamaLad7:MixinExtras:${mixin_extras_version}"
    include "com.github.LlamaLad7:MixinExtras:${mixin_extras_version}"
    annotationProcessor "com.github.LlamaLad7:MixinExtras:${mixin_extras_version}"

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'
    annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'

    implementation(project(path: ":Fabriclike", configuration: "namedElements"))
}

loom {
    accessWidenerPath = file("src/main/resources/bovinesandbuttercups.accesswidener")
    mixin.defaultRefmapName = "bovinesandbuttercups.refmap.json"

    runs {
        client {
            client()
            setConfigName("Quilt Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Quilt Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    from project(":Fabriclike").sourceSets.main.resources

    inputs.property "version", project.version

    filesMatching("quilt.mod.json") {
        expand "version": project.version
    }

    def environment = System.getenv("ENVIRONMENT")
    if (environment != "production") {
        from(rootProject.rootDir.absolutePath + "/testdata") { into 'data' }
        from(rootProject.rootDir.absolutePath + "/testassets") { into 'assets'}
    }
}

tasks.withType(JavaCompile) {
    source(project(":Fabriclike").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials(PasswordCredentials)
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}