plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

archivesBaseName = "${artifact_name}-Fabric"

repositories {
    maven {
        url "https://maven.terraformersmc.com/"
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"

    include(modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}")) {
        exclude(group: "net.fabricmc")
    }
    include(modApi("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}")) {
        exclude(group: "net.fabricmc")
    }

    include(modApi(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-fabric:${mixin_extras_version}")))

    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'
    annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'

    implementation(project(":Common"))

    modCompileOnly("mezz.jei:jei-1.19.4-common-api:${jei_version}")
    modCompileOnly("mezz.jei:jei-1.19.4-fabric-api:${jei_version}")

    // modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}") {
        exclude(group: "net.fabricmc")
    }
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}") {
        exclude(group: "net.fabricmc")
    }

    /*
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}") {
        exclude module: 'dev.architectury'
    }
    modRuntimeOnly "dev.architectury:architectury-fabric:${architectury_version}"
     */

    modCompileOnly "dev.emi:emi-fabric:${emi_version}:api"
    // modLocalRuntime "dev.emi:emi-fabric:${emi_version}"
}

loom {
    accessWidenerPath = file("src/main/resources/bovinesandbuttercups.accesswidener")
    mixin {
        defaultRefmapName = "bovinesandbuttercups-refmap.json"
    }

    mods {
        register(mod_id) {
            sourceSet(project(":Common").sourceSets.main)
            sourceSet(project(":Common").sourceSets.test)
            sourceSet(sourceSets.main)
            sourceSet(sourceSets.test)
        }
    }
    runs {
        client {
            client()
            source(sourceSets.test)
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            source(sourceSets.test)
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

processResources {
    from project(":Common").sourceSets.main.resources

    exclude("bovinesandbuttercups-common.accesswidener")

    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processTestResources {
    outputs.upToDateWhen { false }
    from project(":Common").sourceSets.test.resources
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials(PasswordCredentials)
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}


curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")
    if (apiKey == null) return
    project {
        id = cf_project_id
        changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
        changelogType = 'markdown'
        releaseType = 'release'

        addGameVersion 'Fabric'
        cf_java_versions.split(", ").each { String gameVersion ->
            addGameVersion gameVersion
        }
        fabric_game_versions.split(", ").each { String gameVersion ->
            addGameVersion gameVersion
        }

        mainArtifact(remapJar) {
            displayName = "Fabric v$project.version"
            relations {
                fabric_required_deps.split(", ").each { String project ->
                    requiredDependency project
                }

                common_optional_deps.split(", ").each { String project ->
                    optionalDependency project
                }

                cf_common_optional_deps.split(", ").each { String project ->
                    optionalDependency project
                }

                fabric_optional_deps.split(", ").each { String project ->
                    optionalDependency project
                }

                cf_fabric_embedded_deps.split(", ").each { String project ->
                    embeddedLibrary project
                }
            }
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = mr_project_id
    versionNumber = "${project.version}-fabric"
    versionType = "release"
    versionName = "Fabric v$project.version"
    changelog = new File("$project.rootDir/CHANGELOG.md").getText('UTF-8')
    uploadFile = remapJar
    gameVersions = fabric_game_versions.split(", ").toList()
    loaders = [ "fabric" ]
    dependencies {
        fabric_required_deps.split(", ").each { String project ->
            required.project project
        }

        common_optional_deps.split(", ").each { String project ->
            optional.project project
        }

        mr_common_optional_deps.split(", ").each { String project ->
            optional.project project
        }

        fabric_optional_deps.split(", ").each { String project ->
            optional.project project
        }

        mr_fabric_embedded_deps.split(", ").each { String project ->
            embedded.project project
        }
    }
}